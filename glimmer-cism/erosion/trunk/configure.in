dnl Process this file with autoconf to produce a configure script.

dnl set version
m4_define([erosion_major_version], [0])
m4_define([erosion_minor_version], [5])
m4_define([erosion_version],
	[erosion_major_version.erosion_minor_version])

AC_INIT([erosion],[erosion_version],[Magnus.Hagdorn@ed.ac.uk])
AM_INIT_AUTOMAKE

dnl minimum required glimmer version
min_glimmer_version=0.5.5

EROSION_MAJOR_VERSION=erosion_major_version
EROSION_MINOR_VERSION=erosion_minor_version
EROSION_VERSION=erosion_version
AC_SUBST(EROSION_MAJOR_VERSION)
AC_SUBST(EROSION_MINOR_VERSION)
AC_SUBST(EROSION_VERSION)

AC_PROG_RANLIB
AC_PROG_FC
AC_LANG_PUSH(Fortran)
AC_FC_SRCEXT(f90)
FCFLAGS="$FCFLAGS -fpp"
dnl Checking for python
AM_PATH_PYTHON([2.3],,[AC_MSG_RESULT(need at least python 2.3)])

dnl checking for glimmer prefix
AM_PATH_GLIMMER([$min_glimmer_version])
if test x$GLIMMER_PREFIX = x ;then
  AC_MSG_ERROR([Could not find glimmer])
fi

FCFLAGS="$FCFLAGS $GLIMMER_FFLAGS"
LIBS="$GLIMMER_LIBS $LIBS"

# checking if we build the docs
AC_MSG_CHECKING([whether we should build documentation])
AC_ARG_ENABLE(doc, AS_HELP_STRING([--enable-doc],[Build documentation]),
[case "${enableval}" in
 yes)  AC_MSG_RESULT([yes]);;	
 no)   AC_MSG_RESULT([no]);;
 *) AC_MSG_ERROR(bad value ${enableval} for --enable-doc) ;;
esac],[AC_MSG_RESULT([no])])
AC_CHECK_PROGS(LATEX,[latex]) 
AC_CHECK_PROGS(DVIPS,[dvips]) 
AC_CHECK_PROGS(DVIPDFT,[dvipdfm]) 
AC_CHECK_PROGS(GNUPLOT,[gnuplot]) 
AC_CHECK_PROGS(DIA,[dia]) 
AC_CHECK_PROGS(FIG2DEV,[fig2dev]) 
AC_CHECK_PROGS(PS2EPSI,[ps2epsi]) 
AC_CHECK_PROGS(SED,[sed])
AM_CONDITIONAL([DO_DOC_PS],  [test "$DVIPS" != ""])
AM_CONDITIONAL([DO_DOC_PDF], [test "$DVIPDFT" != ""])
AM_CONDITIONAL([BUILD_DOCS], [test "$enable_doc" == "yes"])

# should we run the tests
AC_MSG_CHECKING([whether we should run the tests])
AC_ARG_ENABLE(tests, AS_HELP_STRING([--enable-tests],[Run short tests]),
[case "${enableval}" in
 yes)  AC_MSG_RESULT([yes]);;
 no)   AC_MSG_RESULT([no]);;
 *) AC_MSG_ERROR(bad value ${enableval} for --enable-tests) ;;
esac],[AC_MSG_RESULT([no])])
AM_CONDITIONAL([RUN_TESTS], [test "$enable_tests" == "yes"])

AC_CONFIG_FILES([Makefile src/Makefile python/Makefile doc/Makefile doc/common/Makefile doc/erosion/Makefile doc/erosion/figs/Makefile doc/erosion/gnu/Makefile tests/Makefile])
AC_OUTPUT([erosion-config src/erosion_version.f90 doc/common/version.tex])
